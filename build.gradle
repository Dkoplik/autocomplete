plugins {
    id 'java-library'
}

group = 'io.github.autocomplete'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform {
        excludeTags 'stress', 'performance'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    description = 'Runs only unit tests.'
}

task stressTest(type: Test) {
    classpath = sourceSets.test.runtimeClasspath
    testClassesDirs = sourceSets.test.output.classesDirs
    useJUnitPlatform {
        includeTags 'stress', 'performance'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
    description = 'Runs only stress and performance tests.'
    group = 'verification'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'io.github.autocomplete',
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version')
        )
    }
}
